/*
 * Copyright(c) OnePoint Software GmbH 2007. All Rights Reserved.
 */

// Controller script for edit_project.oxf.xml

require "onepoint/project/scripts/error.jes";
require "onepoint/project/modules/user/scripts/permissions_tab.jes";
require "onepoint/project/modules/resource/scripts/open_resource_chooser.jes";
require "onepoint/project/modules/project/scripts/helper_project.jes";

import onepoint.express.XValidator;
import onepoint.express.XDisplay;
import onepoint.express.XComponent;
import onepoint.service.XMessage;

function ok(event) {
   form = XDisplay.getActiveForm();
   edit_mode = form.findComponent("EditMode").getBooleanValue();
   if (edit_mode) {
      ok_edit(event);
   } else {
      cancel(event);
   }
}

function ok_edit(event) {
   bypassFutureStartDateCheck = event.BypassFutureStartDateCheck;

   // Gather project-data
   form = XDisplay.getActiveForm();

   //check for future start dates
   project_data.Start = form.findComponent("Start").getDateValue();
   originalStartDate = form.findComponent("OriginalStartDate").getDateValue();
   if (bypassFutureStartDateCheck == null && project_data.Start.after(originalStartDate)) {
      Console.println("Start date in the future");
      dialog = XComponent.newDialog();
      dialog.loadForm("/modules/project/forms/confirm_startdate_future.oxf.xml");
      dialog.open();
      return;
   }

   project_id = form.findComponent("ProjectID").getStringValue();
   project_data.Name = form.findComponent("Name").getStringValue();
   project_data.Description = form.findComponent("Description").getStringValue();
   project_data.Finish = form.findComponent("Finish").getDateValue();
   project_data.Budget = form.findComponent("Budget").getDoubleValue();
   project_data.CalculationMode = form.findComponent("CalculationMode").getBooleanValue();
   project_data.ProgressTracked = form.findComponent("ProgressTracked").getBooleanValue();
   choice = form.findComponent("StatusChoice").getStringValue();
   if (choice != null) {
      project_data.Status = XValidator.choiceID(choice);
   }

   goals_set = XDisplay.getActiveForm().findComponent("GoalsSet");
   to_dos_set = XDisplay.getActiveForm().findComponent("ToDosSet");
   assigned_resource_set = XDisplay.getActiveForm().findComponent("AssignedResourceDataSet");
   original_resource_set = XDisplay.getActiveForm().findComponent("OriginalResourceDataSet");
   versions_set = XDisplay.getActiveForm().findComponent("VersionsSet");

   project_data.PermissionSet = form.findComponent("PermissionSet");

   //if the hourlyRates tab was modified that is the priority
   modifiedPeriods = isModifiedHourlyTab();
    if (modifiedPeriods.size() > 0) {
      request = new XMessage;
      request.setAction("ProjectService.haveAssignmentsInTimePeriod");
      request.setArgument("resource_set", assigned_resource_set);
      request.setArgument("project_id", project_id);
      response = XDisplay.getClient().invokeMethod(request);
      hasAssignments = response.getArgument("AssignmentsInPeriod");
      if (hasAssignments == true) {
         //display
         dialog = XComponent.newDialog();
         parameters.project_id = project_id;
         parameters.project_data = project_data;
         parameters.goals_set = goals_set;
         parameters.to_dos_set = to_dos_set;
         parameters.resource_set = assigned_resource_set;
         parameters.versions_set = versions_set;
         parameters.changed = "hourlyTab";
         dialog.open("/modules/project/forms/confirm_assignment_changed.oxf.xml", parameters);
         return;
      }
    }

    //check if the rates from the resource tab were modified
   request = new XMessage;
   request.setAction("ProjectService.checkModifiedRates");
   request.setArgument("resource_set", assigned_resource_set);
   request.setArgument("original_resource_set", original_resource_set);
   request.setArgument("project_id", project_id);
   response = XDisplay.getClient().invokeMethod(request);
   hasAssignments = response.getArgument("Assignments");
   if (hasAssignments == true) {
      //display
      dialog = XComponent.newDialog();
      parameters.project_id = project_id;
      parameters.project_data = project_data;
      parameters.goals_set = goals_set;
      parameters.to_dos_set = to_dos_set;
      parameters.resource_set = assigned_resource_set;
      parameters.versions_set = versions_set;
      parameters.changed = "resourceTab";
      dialog.open("/modules/project/forms/confirm_assignment_changed.oxf.xml", parameters);
      return;
   }

   request = new XMessage;
   request.setAction("ProjectService.updateProject");
   request.setArgument("project_id", project_id);
   request.setArgument("project_data", project_data);
   request.setArgument("goals_set", goals_set);
   request.setArgument("to_dos_set", to_dos_set);
   request.setArgument("resource_set", assigned_resource_set);
   request.setArgument("versions_set", versions_set);
   response = XDisplay.getClient().invokeMethod(request);

   if (handleError(form, response, "ErrorLabel") == true) {
      XDisplay.getActiveWindow().close();
      XDisplay.findFrame("MainFrame").refreshForm();
   }
}

function cancel(event) {
   XDisplay.getActiveWindow().close();
}

function removeVersions(event) {
   data_set = XDisplay.getActiveForm().findComponent("VersionsSet");
   selected_rows = data_set.selectedRows();
   if (selected_rows.size() > 0) {
      dialog = XComponent.newDialog();
      header = XDisplay.getActiveForm().findComponent("ConfirmDialogHeader").getText();
      dialog.setText(header);
      form = dialog.loadForm("/modules/project/forms/confirm_version_delete.oxf.xml");
      form.findComponent("SelectedVersions").setListValue(selected_rows);
      dialog.open();
   }
}

function selectionChanged(event) {

   form = XDisplay.getActiveForm();
   version_number = form.findComponent("WorkingVersionNumber");
   version_set = form.findComponent("VersionsSet");
   selected_rows = version_set.selectedRows();

   delete_button = form.findComponent("RemoveVersionButton");
   delete_button.setEnabled(true);

   can_delete = true;
   //determine if the working version is among the selected ones
   if (selected_rows.size() > 0) {
      
      i = 0;
      while (i < selected_rows.size()) {
         if (selected_rows[i].getChild(0).getStringValue() == version_number.getStringValue()) {
            can_delete = false;
            break;
         }
         i++;
      }
   }

   if (!can_delete) {
      delete_button = form.findComponent("RemoveVersionButton");
      delete_button.setEnabled(can_delete);
   }
   form.repaint();
}

//checks if any of the OpHourlyRatesPeriod from the Hourly Rates tab was modified and
//returns all modified OpHourlyRatesPeriods
function isModifiedHourlyTab() {
   newRatesDataSet = XDisplay.getActiveForm().findComponent("AssignedResourceDataSet");
   originalRatesDataSet = XDisplay.getActiveForm().findComponent("OriginalResourceDataSet");
   modifiedRates = new ArrayList;

   //add to modified rows the original rows that have changed
   i = 0;
   while (i < originalRatesDataSet.getChildCount()) {
      modified = true;
      originalRow = originalRatesDataSet.getChild(i);
      if (originalRow.getOutlineLevel() == 1) {
         j = 0;
         while (j < newRatesDataSet.getChildCount()) {
            newRow = newRatesDataSet.getChild(j);
            if (newRow.getOutlineLevel() == 1) {
               if (originalRow.getChild(5).getDateValue() == newRow.getChild(5).getDateValue() &&
                   originalRow.getChild(6).getDateValue() == newRow.getChild(6).getDateValue() &&
                   originalRow.getChild(7).getDoubleValue() == newRow.getChild(7).getDoubleValue() &&
                   originalRow.getChild(8).getDoubleValue() == newRow.getChild(8).getDoubleValue()) {
                  modified = false;
                  break;
               }
            }
            j++;
         }
         if (modified) {
            modifiedRates.add(originalRow);
         }
      }
      i++;
   }
   //add to modified rows the newly changed or added rows
   i = 0;
   while (i < newRatesDataSet.getChildCount()) {
      modified = true;
      newRow = newRatesDataSet.getChild(i);
      if (newRow.getOutlineLevel() == 1) {
         j = 0;
         while (j < originalRatesDataSet.getChildCount()) {
            originalRow = originalRatesDataSet.getChild(j);
            if (originalRow.getOutlineLevel() == 1) {
               if (originalRow.getChild(5).getDateValue() == newRow.getChild(5).getDateValue() &&
                   originalRow.getChild(6).getDateValue() == newRow.getChild(6).getDateValue() &&
                   originalRow.getChild(7).getDoubleValue() == newRow.getChild(7).getDoubleValue() &&
                   originalRow.getChild(8).getDoubleValue() == newRow.getChild(8).getDoubleValue()) {
                  modified = false;
                  break;
               }
            }
            j++;
         }
         if (modified) {
            modifiedRates.add(newRow);
         }
      }
      i++;
   }
   return modifiedRates;
}